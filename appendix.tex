\section{Appendix} \label{appendix}


\input{latex/twinsigma}

\subsection{cubicalTT} \label{cubicaltt}
\begin{verbatim}
abstract Exp = {

flags startcat = Decl ;
      -- note, cubical tt doesn't support inductive families, and therefore the datatype (& labels) need to be modified

cat
  Comment ;
  Module  ;
  AIdent ;
  Imp ; --imports, add later
  Decl ;
  Exp ;
  ExpWhere ;
  Tele ;
  Branch ;
  PTele ;
  Label ;
  [AIdent]{0} ; -- "x y z"
  [Decl]{1} ;
  [Tele]{0} ;
  [Branch]{1} ;
  [Label]{1} ;
  [PTele]{1} ;
  -- [Exp]{1};

fun

  DeclDef : AIdent -> [Tele] -> Exp -> ExpWhere -> Decl ;
  -- foo ( b : bool ) : bool = b
  DeclData : AIdent -> [Tele] -> [Label] -> Decl ;
  -- data nat : Set where zero | suc ( n : nat )
  DeclSplit : AIdent -> [Tele] -> Exp -> [Branch] -> Decl ;
  -- caseBool ( x : Set ) ( y z : x ) : bool -> Set = split false -> y || true -> z
  DeclUndef : AIdent -> [Tele] -> Exp -> Decl ;
  -- funExt ( a : Set ) ( b : a -> Set ) ( f g : ( x : a ) -> b x ) ( p : ( x : a ) -> ( b x ) ( f x ) == ( g x ) ) : ( ( y : a ) -> b y ) f == g = undefined

  Where : Exp -> [Decl] -> ExpWhere ;
  -- foo ( b : bool ) : bool =
  -- f b where f : bool -> bool = negb
  NoWhere : Exp -> ExpWhere ;
  -- foo ( b : bool ) : bool =
  -- b

  Split : Exp -> [Branch] -> Exp ;
  --split@ ( nat -> bool ) with zero  -> true || suc n -> false
  Let : [Decl] -> Exp -> Exp ;
  -- foo ( b : bool ) : bool =
  -- let f : bool -> bool = negb in f b
  Lam : [PTele] -> Exp -> Exp ;
  -- \\ ( x : bool ) -> negb x
  -- todo, allow implicit typing
  Fun : Exp -> Exp -> Exp ;
  -- Set -> Set
  -- Set -> ( b : bool ) -> ( x : Set ) -> ( f x )
  Pi    : [PTele] -> Exp -> Exp ;
  --( f : bool -> Set ) -> ( b : bool ) -> ( x : Set ) -> ( f x )
  -- ( f : bool -> Set ) ( b : bool ) ( x : Set ) -> ( f x )
  Sigma : [PTele] -> Exp -> Exp ;
  -- ( f : bool -> Set ) ( b : bool ) ( x : Set ) * ( f x )
  App : Exp -> Exp -> Exp ;
  -- proj1 ( x , y )
  Id : Exp -> Exp -> Exp -> Exp ;
  -- Set bool == nat
  IdJ : Exp -> Exp -> Exp -> Exp -> Exp -> Exp ;
  -- J e d x y p
  Fst : Exp -> Exp ; -- "proj1 x"
  Snd : Exp -> Exp ; -- "proj2 x"
  -- Pair : Exp -> [Exp] -> Exp ;
  Pair : Exp -> Exp -> Exp ;
  -- ( x , y )
  Var : AIdent -> Exp ;
  -- x
  Univ : Exp ;
  -- Set
  Refl : AIdent ; -- Exp ;
  -- refl
  --Hole : HoleIdent -> Exp ; -- need to add holes

  OBranch :  AIdent -> [AIdent] -> ExpWhere -> Branch ;
  -- suc m -> split@ ( nat -> bool ) with zero -> false || suc n -> equalNat m n
  -- for splits

  OLabel : AIdent -> [Tele] -> Label ;
  -- suc ( n : nat )
  -- fora data types

  -- construct telescope
  TeleC : AIdent -> [AIdent] -> Exp -> Tele ;
  -- "( f g : ( x : a ) -> b x )"
  -- ( a : Set ) ( b : ( a ) -> ( Set ) ) ( f g : ( x : a ) -> ( ( b ) ( x ) ) ) ( p : ( x : a ) -> ( ( ( b ) ( x ) ) ( ( f ) ( x ) ) == ( ( g ) ( x ) ) ) )

  -- why does gr with this fail so epically?
  PTeleC : Exp -> Exp -> PTele ; 
  -- ( x : Set ) -- ( y : x -> Set )" -- ( x : f y z )"

  --everything below this is just strings

  Foo : AIdent ;
  A , B , C , D , E , F , G , H , I , J , K , L , M , N , O , P , Q , R , S , T , U , V , W , X , Y , Z : AIdent ;
  True , False , Bool : AIdent ;
  NegB : AIdent ;
  CaseBool : AIdent ;
  IndBool : AIdent ;
  FunExt : AIdent ;
  Nat : AIdent ;
  Zero : AIdent ;
  Suc : AIdent ;
  EqualNat : AIdent ;
  Unit : AIdent ;
  Top : AIdent ;
  Contr : AIdent ;
  Fiber : AIdent ;
  IsEquiv : AIdent ;
  IdIsEquiv : AIdent ;
  IdFun : AIdent ;
  ContrSingl : AIdent ;
  Equiv : AIdent ;
  EqToIso : AIdent ;
  Ybar : AIdent ;
  IdFib : AIdent ;
  Identity : AIdent ;
  Lemma0 : AIdent ;
}
\end{verbatim}
\begin{verbatim}
concrete ExpCubicalTT of Exp = open Prelude, FormalTwo in {

lincat 
  Comment,
  Module ,
  AIdent,
  Imp,
  Decl ,
  ExpWhere,
  Tele,
  Branch ,
  PTele,
  Label,
    -- = Str ;
  [AIdent],
  [Decl] ,
  -- [Exp],
  [Tele],
  [Branch] ,
  [PTele],
  [Label]
    -- = {hd,tl : Str} ;
    = Str ;
  Exp = TermPrec ;

lin

  DeclDef a lt e ew = a ++ lt ++ ":" ++ usePrec 0 e ++ "=" ++ ew ;
  DeclData a t d = "data" ++ a ++ t ++ ": Set where" ++ d ;
  DeclSplit ai lt e lb = ai ++ lt ++ ":" ++ usePrec 0 e ++ "= split" ++ lb ;
  DeclUndef a lt e = a ++ lt ++ ":" ++ usePrec 0 e ++ "= undefined" ; -- postulate in agda

  Where e ld = usePrec 0 e ++ "where" ++ ld ;
  NoWhere e = usePrec 0 e ;

  Let ld e = mkPrec 0 ("let" ++ ld ++ "in" ++ (usePrec 0 e)) ;
  Split e lb = mkPrec 0 ("split@" ++ usePrec 0 e ++ "with" ++ lb) ;
  Lam pt e = mkPrec 0 ("\\" ++ pt ++ "->" ++ usePrec 0 e) ;
  Fun = infixr 1 "->" ; -- A -> Set
  Pi pt e = mkPrec 1 (pt ++ "->" ++ usePrec 1 e) ;
  Sigma pt e = mkPrec 1 (pt ++ "*" ++ usePrec 1 e) ;
  App = infixl 2 "" ;
  Id e1 e2 e3 = mkPrec 3 (usePrec 4 e1 ++ usePrec 4 e2 ++ "==" ++ usePrec 3 e3) ;
-- for an explicit vs implicit use of parameters, may have to use expressions as records, with a parameter is_implicit
  IdJ e1 e2 e3 e4 e5 = mkPrec 3 ("J" ++ usePrec 4 e1 ++ usePrec 4 e2 ++ usePrec 4 e3 ++ usePrec 4 e4 ++ usePrec 4 e5) ;
  Fst e = mkPrec 4 ("fst" ++ usePrec 4 e) ;
  Snd e = mkPrec 4 ("snd" ++ usePrec 4 e) ;
  Pair e1 e2 = mkPrec 5 ("(" ++ usePrec 0 e1 ++ "," ++ usePrec 0 e2 ++ ")") ;
  Var a = constant a ;
  Univ = constant "Set" ;
  Refl = "refl" ; -- constant "refl" ;

  BaseAIdent = "" ;
  ConsAIdent x xs = x ++ xs ;

  -- [Decl] only used in ExpWhere
  BaseDecl x = x ;
  ConsDecl x xs = x ++ "^" ++ xs ;

  -- maybe accomodate so split on empty type just gives () 
  -- BaseBranch = "" ;
  BaseBranch x = x ;
  -- ConsBranch x xs = x ++ "\n" ++ xs ;
  ConsBranch x xs = x ++ "||" ++ xs ;

  -- for data constructors
  BaseLabel x = x ;
  ConsLabel x xs = x ++ "|" ++ xs ; 

  BasePTele x = x ;
  ConsPTele x xs = x ++ xs ;

  BaseTele = "" ;
  ConsTele x xs = x ++ xs ;

  OBranch a la ew = a ++ la ++ "->" ++ ew ;
  TeleC a la e = "(" ++ a ++ la ++ ":" ++ usePrec 0 e ++ ")" ;
  PTeleC e1 e2 = "(" ++ top e1 ++ ":" ++ top e2 ++ ")" ;

  OLabel a lt = a ++ lt ;

  --object language syntax, all variables for now
  Bool = "bool" ;
  True = "true" ;
  False = "false" ;
  CaseBool = "caseBool" ;
  IndBool = "indBool" ;
  FunExt = "funExt" ;
  Nat = "nat" ;
  Zero = "zero" ;
  Suc = "suc" ;
  EqualNat = "equalNat" ;
  Unit = "unit" ;
  Top = "top" ;
  Foo = "foo" ; 
  A = "a" ;
  B = "b" ;
  C = "c" ;
  D = "d" ;
  E = "e" ;
  F = "f" ;
  G = "g" ;
  H = "h" ;
  I = "i" ;
  J = "j" ;
  K = "k" ;
  L = "l" ;
  M = "m" ;
  N = "n" ;
  O = "o" ;
  P = "p" ;
  Q = "q" ;
  R = "r" ;
  S = "s" ;
  T = "t" ;
  U = "u" ;
  V = "v" ;
  W = "w" ;
  X = "x" ;
  Y = "y" ;
  Z = "z" ;
  NegB = "negb" ;
  -- everything below is for contractible proofs
  Contr = "isContr" ;
  Fiber = "fiber" ;
  IsEquiv = "isEquiv" ;
  IdIsEquiv = "idIsEquiv" ;
  IdFun = "idfun" ;
  ContrSingl = "contrSingl" ;
  Equiv = "equiv" ;
  EqToIso = "eqToIso" ;
  Identity = "id" ;
  Ybar = "ybar"  ;
  IdFib = "idFib"  ;
  Lemma0 = "lemma0" ;
}
\end{verbatim}
The resource FormalTwo.gf merely substitutes more precedences than Formal.gf
from the RGL, in the ideal case that we could scale the grammar to include
larger and more complicated fixity information.
\begin{verbatim}
resource FormalTwo = open Prelude in {

----Everything the same up until the definition of Prec in Formal.gf


    Prec : PType = Predef.Ints 9 ;

    highest = 9 ;

    lessPrec : Prec -> Prec -> Bool = \p,q ->
      case <<p,q> : Prec * Prec> of {
        <3,9> | <2,9> | <4,9> | <5,9> | <6,9> | <7,9> | <8,9> => True ;
        <3,8> | <2,8> | <4,8> | <5,8> | <6,8> | <7,8> => True ;
        <3,7> | <2,7> | <4,7> | <5,7> | <6,7> => True ;
        <3,6> | <2,6> | <4,6> | <5,6> => True ;
        <3,5> | <2,5> | <4,5> => True ;
        <3,4> | <2,3> | <2,4> => True ;
        <1,1> | <1,0> | <0,0> => False ;
        <1,_> | <0,_>         => True ;
        _ => False
      } ;

    nextPrec : Prec -> Prec = \p -> case <p : Prec> of {
      9 => 9 ;
      n => Predef.plus n 1
      } ;
\end{verbatim}


\input{latex/compare}

\subsection{HoTT Agda Corpus} \label{hottproofs}